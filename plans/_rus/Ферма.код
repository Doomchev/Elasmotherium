предметы.максимум = 2

идти(НачалоИгры)

объект Горящ[ий]Факел[] это Предмет
	Команда("осмотреть факел")
    выполнить() -> сообщить("небольшой факел, совершенно обыкновенный на вид.")

	Команда("погасить факел") 
    выполнить()
      сообщить("OК, он сразу же погас.")
      ГорящийФакел.скрыть()
      Факел.взять()

объект Деревянн[ые]Щепк[и] это Предмет

объект Крупн[ый]Алмаз[] это Предмет
	Команда("осмотреть алмаз")
		выполнить() -> сообщить("Что за подозрения! Он настоящий. Скорей бери его и покончим с этим делом!")

действ[ия]Перед()
	если(КрупныйАлмаз.вИнвентаре) конец("Поздравляем! Вы закончили свою миссию.")

Локация.описание(объект1)
	сообщить("Вы не видите ничего необычного.")



объект Начал[о]Игры это Локация
	описание()
		сообщить("Цель этой короткой  программы очень проста. Вам надо разыскать крупный алмаз и завладеть им.")
		сообщить("")
		сообщить("*Начать игру*")
		сообщить("*Авторы*")
		
	Команда("начать игру")
		выполнить() -> идти(Поле)
		
	Команда("авторы")
		выполнить() -> идти(Авторов)
	


объект Автор[ы] это Локация
	описание()
		сообщить("ФЕРМА")
		сообщить("Демонстрационная программа Adventure Building System")
		сообщить("Оригинальная концепция: T. D. Frost")
		сообщить("Перевод на русский язык: Инфорком")
		сообщить("Конверсия, разработка платформы: Матвей Меркулов")
		сообщить("Иллюстрации: Анастасия Елпанова")
		сообщить("")
		сообщить("*Возврат*")
		
	Команда("возврат")
		выполнить() -> идти(НачалоИгры)
	


объект Амбар[] это Локация
	объект Коротк[ая]Лопат[а] это Предмет
		
	описание()
		изображение("")
		сообщить("Вы находитесь в чистом и аккуратном амбаре. Фермер, которому он принадлежит, по-видимому любит свое дело. Можно *идти на юг*.")
		
	Выход("идти на юг", Двор)
	


объект Чердак[] это Локация
	описание()
		изображение("attic.jpg")
		сообщить("Забравшись по *лестнице* вверх, вы попали как бы на открытый чердак. Скорее его можно описать, как большое хранилище над комнатой.")
		
  Выход("лестнице/спуститься вниз", Конюшня)



объект Двор[] это Локация
	описание()
		изображение("")
		сообщить("Вы вышли на двор фермы. Никаких животных не видно. На севере - *амбар*, на юге - *поле*.")
		
  Выход("амбар/идти к амбару", Амбар)
  Выход("поле/идти в поле", Поле)
	


объект Гостиная[] это Локация
	объект Шерстян[ой]Плед это Одежда
		Команда("осмотреть плед") -> сообщить("Красивый плед, выглядит как из французской шерсти.")
				
	описание()
		изображение("")
		сообщить("Уютная гостиная крестьянского дома. Вы чувствуете на себе чей-то взгляд. Двери ведут в *столовую* и в *прихожую*.")
		
  Выход("столовую/идти в столовую", Столовая)
  Выход("прихожую/идити в прихожую", Прихожая)
	


объект Столов[ая] это Локация
	описание()
		сообщить("Кажется, что этой столовой комнатой давно никто не пользовался. Наверное, здесь никто не живет. Отсюда можно пройти *на кухню* и *в гостиную*.")
		
  Выход("на кухню", Кухня)
  Выход("в гостиную", Гостиная)
	


объект Конюшн[я] это Локация
	описание()
		изображение("stable.jpg")
		сообщить("Конюшня совершенно пуста. Нет даже характерного запаха животных. Лестница ведет *на чердак*, видны также *ступени*, скрывающиеся где-то внизу, в темноте. Можно *выйти* из конюшни.")
		
  Выход("выйти", Поле)
  Выход("на чердак", Чердак)
  Выход("ступени/спуститься вниз", Погреб)
	


объект Пол[е] это Локация
	Выкопан[а]Ям[а] = нет
	
	описание()
		изображение("field.jpg")
		сообщить("Вы вышли в открытое *поле*. Оно было недавно вспахано, но ничего посажено не было. Можно пойти *на север*, *на юг*, *на восток* и *на запад*.")
		
  Выход("на север", Двор)
  Выход("на юг", Дорога)
  Выход("на восток", Конюшня)
  Выход("на запад", Порог)
		
	Команда("поле/выкопать яму лопатой")
    условие() -> !выкопанаЯма и КороткаяЛопата.вИнвентаре
    выполнить()
      сообщить("ОК, вы выкопали яму в поле.")
      выкопанаЯма = да
		
	Команда("яма/закопать яму лопатой")
    условие() -> выкопанаЯма и КороткаяЛопата.вИнвентаре
    выполнить()
      сообщить("OK, вы закопали яму в поле.")
      выкопанаЯма = нет
		
	Команда("поле/осмотреть поле")
    выполнить()
      если(выкопанаЯма)
        сообщить("Здесь *яма*, будьте ocтopoжны!")
      иначе
        сообщить("Чувствуeтcя заботливый уход, нo недавний дождь оставил раскисшую почву!")
			


объект Порог[] это Локация
	объект Двер[ь]
		открыт[а] = нет
		
		Команда("войти в дверь")
      условие() -> открыта
			выполнить() -> идти(Прихожую)
				
		Команда("осмотреть дверь")
			выполнить() -> сообщить("Дверь сделана из добротного дерева и в данный момент $если(открыта тогда "остается широко открытой" иначе "закрыта, но не заперта").")
				
		Команда("открыть дверь")
      условие() -> !открыта
      выполнить()
        сообщить("ОК, вы открыли дверь.")
        открыта = да
				
		Команда("закрыть дверь")
      условие() -> открыта
      выполнить()
        сообщить("OК, теперь дверь закрыта.")
        открыта = нет
				
	описание()
		изображение("door_$если(Дверь.открыта тогда "opened" иначе "closed").jpg")
		сообщить("Вы находитесь у двери фермерского дома. Дом сделан очень добротно и крепко, как будто хозяевам было от чего скрываться. От двери отходит дорога в *поле*.")
		
  Выход("поле/идти в поле", Поле)
	


объект Прихож[ая] это Локация
	объект Буфет[]
		открыт[] = нет
		открывал[ся] = нет

		Команда("открыть буфет")
      условие() -> !открыт
      выполнить()
        открыт = да
        сообщить("ОК, вы открыли буфет.")
        если(!открывался)
          сообщить("Вы слышите тихий голос, доносящийся из глубины буфета: ''швырни шкатулку c чердака и увидишь, что будет''.")
          открывался = да
        иначе
          сообщить("Но что такое? Всё тихо!")
							
		Команда("закрыть буфет")
      условие() -> открыт
			выполнить() 
        открыт = нет
        сообщить("ОК, теперь буфет закрыт.")
			
	описание()		
		сообщить("Перед вами небольшая прихожая. В углу стоит $если(Буфет.открыт тогда "открытый" иначе "закрытый") буфет и больше ничего здесь нет. На востоке - *кухня*, на севере - *гостиная*. Также можно *выйти из дома*.")
	
  Выход("кухня/идти на кухню", Кухня)
  Выход("гостиная/идти в гостиную", Гостиная)
  Выход("выйти из дома", Порог)



объект Кухн[я] это Локация
	объект Факел[] это Предмет
		Команда("осмотреть факел")
			выполнить() -> сообщить("Небольшой факел, совершенно обыкновенный на вид.")
		
		Команда("зажечь факел")
      выполнить()
        сообщить("OK, теперь факел горит.")
        Факел.скрыть()
        ГорящийФакел.взять()
				
	описание()
		изображение("kitchen.jpg")
		сообщить("Кухня сверкает белизной и чистотой. В воздухе определенно чувствуется запах чего-то приятного. Можно идти *в прихожую*, *в столовую* или *в сад*.")
		
  Выход("в прихожую", Прихожая)
  Выход("в столовую", Столовая)
  Выход("в сад", Сад)
	


объект Деревянн[ая]Шкатулк[а] это Предмет
  Команда("осмотреть шкатулку")
    выполнить() -> сообщить("Она достаточно прочна.")
  
  Команда("открыть шкатулку")
    выполнить() -> сообщить("Несмотря ни на какие усилия открыть ее не удается!")
  
  Команда("отпереть шкатулку/отпереть шкатулку бронзовым ключом")
    условие() -> БронзовыйКлюч.вИнвентаре
    выполнить() -> сообщить("Ключ к шкатулке нe подходит.")
        
  Команда("швырнуть шкатулку с чердака")
    условие() -> Текущийобъект == Чердак
    выполнить()
      сообщить("Вот это бросок!!! От удара шкатулка разбилась. Что-то из нее выпало и, блеснув, покатилось по полу конюшни."
      ДеревяннуюШкатулку.скрыть()
      ДеревянныеЩепки.переместитьВ(Конюшню)
      КрупныйАлмаз.переместитьВ(Конюшню)



объект Сейф[]
  открыт[] = нет
  заперт[] = да
  
  Команда("отпереть сейф/отпереть сейф ключом")
    условие() -> БронзовыйКлюч.вИнвентаре и Сейф.заперт
    выполнить()
      сообщить("ОК, вы отперли сейф ключом."
      заперт = нет
      
  Команда("запереть сейф/запереть сейф ключом")
    условие() -> БронзовыйКлюч.вИнвентаре и !Сейф.заперт
    выполнить()
      если(Сейф.открыт)
        сообщить("Сейф в данный момент открыт, его нельзя запереть.")
      иначе
        сообщить("ОК, вы заперли сейф на ключ.")
        заперт = да
            
  Команда("открыть сейф")
    условие() -> !Сейф.открыт
    выполнить()
      если(Сейф.заперт)
        сообщить("Не открывается! заперт на ключ.")
      иначе если(Сейф.содержит(ДеревяннуюШкатулку))
        сообщить("Сейф открывается, и внутри вы видите деревянную шкатулку.")
      иначе
        сообщить("ОК, вы открыли сейф.")
        открыт = да
            
  Команда("закрыть сейф")
    условие() -> Сейф.открыт
    выполнить()
      сообщить("ОК, вы закрыли дверь сейфа.")
      открыт = нет
      
  Команда("осмотреть сейф")
    выполнить()
      если(Сейф.открыт)
        сообщить("Дверца сейфа открыта.")
      иначе если(Сейф.заперт)
        сообщить("Сейф заперт на ключ.")
      иначе
        сообщить("Дверца сейфа закрыта, но не заперта.")



объект Погреб[] это Локация
  ДеревяннаяШкатулка
  Сейф

	описание()
		если(ГорящийФакел.вИнвентаре или ГорящийФакел.вЛокации(Погреб))
			изображение("basement_$если(Сейф.открыт тогда если(Сейф.содержит(ДеревяннуюШкатулку) тогда "casket" иначе "empty") иначе "lit").jpg")
			сообщить("В отличие от большинства обычных погребов, этот явно не используется для хранения всяких запасов. А в углу можно разглядеть большой старомодный сейф со встроенным в него бронзовым замком. Можно взобраться обратно *по лестнице*.")
			Сейф.скрыт = нет
		иначе
			изображение("basement_dark.jpg")
			сообщить("Здесь темно и ничего не видно! Только на *лестницу* падает немного света.")
			Сейф.скрыт = да
				
	Выход("по лестнице", Конюшня)
	Выход("лестницу/подняться по лестнице", Конюшня)
	


объект Тупик[] это Локация
	описание()
		изображение("dead_end.jpg")
		сообщить("Это тупик. Можно было сюда вообще не приходить. Вернёмся *назад*?")
		
  Выход("назад", Дорога)
	


объект Дорог[а] это Локация
	описание()
		изображение("road.jpg")
		сообщить("Эта очень узкая дорога ведет с *востока* на *запад*. Но, кажется, можно пройти и на *север*.")
		
  Выход("востока/идти на восток", Тупик)
  Выход("запад/идти на запад", УВорот)
  Выход("север", Поле)
	



объект Ворот[а]
  открыт[ы] = нет

  Команда("войти в ворота")
    условие() -> Ворота.открыты и локация == УВорот
    выполнить() -> идти(Сад)
      
  Команда("перелезть через ворота")
    условие() -> !Ворота.открыты
    выполнить()
      если(локация == Сад)
        идти(УВорот)
      иначе
        идти(Сад)
            
  Команда("осмотреть ворота")
    выполнить()
      сообщить("Bopoтa сделаны из неважного дерева и в данный момент $если(Ворота.открыты тогда "остаются открытыми" иначе "закрыты изнутри на засов").")
      
  Команда("выйти за ворота")
    условие() -> Ворота.открыты и локация == Сад
    выполнить() -> идти(УВорот)
  
  Команда("открыть ворота")
    условие() -> !Ворота.открыты и локация == Сад
    выполнить()
      Ворота.открыты = да
      сообщить("ОК, вы открыли ворота, отодвинув засов.")
      
  Команда("закрыть ворота")
    условие() -> Ворота.открыты и локация == Сад
    выполнить()
      Ворота.открыты = нет
      сообщить("ОК, вы закрыли ворота на засов.")



объект УВорот это Локация
  Ворота
  
	описание()
		изображение("")
		сообщить("*Дорога* упирается в ворота, за ними расположен приусадебный сад.")
		
  Выход("дорога/пойти по дороге", Дорога)
  
  
  
объект Ям[а]
  скрыт[а] = да

  объект Бронзовый[]Ключ[] это Предмет
  
  Команда("осмотреть яму")
    выполнить() -> сообщить("Это довольно глубокая яма, и если туда свалиться, то...!")
    
  Команда("закопать яму")
    выполнить() -> сообщить("Для этого нужен подходящий инструмент.")
      
  Команда("закопать яму/закопать яму лопатой")
    условие() -> КороткаяЛопата.вИнвентаре()
    выполнить()
      сообщить("OК, вы закопали яму и оставили на лужайке некрасивое пятно!")
      Яма.скрыта = да



объект Сад[] это Локация
	Яма
	Ворота
	
	описание()
		изображение "garden$если(Ворота.открыты тогда "_open" иначе "")$если(!Яма.скрыта тогда "_hole" иначе "").jpg"
		сообщить("Это довольно пустынный *сад*, который скорее похож на лужайку с несколькими цветочными клумбами. $если(Яма.скрыта" тогда "Здесь было бы совсем красиво, если бы не бесцветное вытоптанное *пятно* перед воротами, ведущими на запад." иначе "Вы видите ворота, ведущие на запад. Можно *войти в дом*.")
		
  Выход("войти в дом", Кухня)
		
	Команда("сад/осмотреть сад")
    условие() -> !Яма.скрыта
		выполнить() -> сообщить("Здесь $СсылкаНа(Яму, ИменительныйПадеж), будьте ocтopoжны!")
		
	Команда("сад/осмотреть сад")
    условие() -> Яма.скрыта
		выполнить() -> сообщить("Вы не видите ничего необычного.")
		
	Команда("пятно/осмотреть пятно")
		сообщить("Выглядит так, как будто здесь кто-то копал.")
		
	Команда("пятно/копать яму")
		сообщить("Руками копать не получается. Для этого нужен подходящий инструмент.")
		
	Команда("пятно/копать яму/копать яму лопатой")
    условие() -> вИнвентаре(КороткаяЛопата)
    выполнить()
      если(Яма.содержит(БронзовыйКлюч))
        сообщить("Вы испортили красивую лужайку глубокой ямой. Но вот что-то блестнуло... Это ключ!")
      иначе
        сообщить("OK, вы выкопали яму в саду.")
        Яма.скрыта = нет
	