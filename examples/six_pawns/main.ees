board = Texture("board.png")
tileMap = TileMap(7, 1, Texture("pawns.png").cut(3))

empty = 0
white = 1
black = 2

rules() {
	showMessage("Вам нужно поменять черные и белые пешки местами.\nЧерные пешки ходят влево, белые - вправо.\n"
			+ "Пешка может пойти на одну клетку вперед\nили перепрыгнуть через следующую пешку\nна свободное поле."
			, "Правила игры")
}

init() {
	for(n = 0 ..< tileMap.cellXQuantity) tileMap[n, 0] = n <= 2 ? white : (n >= 4 ? black : empty)
}

init()
rules()

Window("Six pawns", tileMap) {
	render() {
		tileMapX = (wnd.width - tileMap.cellWidth * 7) / 2
		tileMapY = (wnd.height - tileMap.cellHeight) / 2 - 31
		boardX = (wnd.width - board.width) / 2
		boardY = (wnd.height - board.height) / 2 + 31
		
		board.draw(boardX, boardY)
		$tileMap.draw(tileMapX, tileMapY)
	}

	onClick(x, y) {
		with(tileMap) {
			tileNum = limit($getTileX(x - tileMapX), 0 .. $cellXQuantity - 1)
			tile = $[tileNum, 0]
			if(tile == white && tileNum < $cellXQuantity - 1) {
				nextTile = $[tileNum + 1, 0]
				if(nextTile == empty) {
					$[tileNum, 0] = empty
					$[tileNum + 1, 0] = tile
				} else if(tileNum < $cellXQuantity - 2 && $[tileNum + 2, 0] == empty) {
					$[tileNum, 0] = empty
					$[tileNum + 2, 0] = tile
				}
			} else if(tile == black && tileNum > 0) {
				prevTile = $[tileNum - 1, 0]
				if(prevTile == empty) {
					$[tileNum, 0] = empty
					$[tileNum - 1, 0] = tile
				} else if(tileNum > 1 && $[tileNum - 2, 0] == empty) {
					$[tileNum, 0] = empty
					$[tileNum - 2, 0] = tile
				}
			}
			
			if($[0, 0] == white) return
			for(n = 1 ..< $cellXQuantity) {
				if($[n - 1, 0] == ($[n, 0] == white ? black : white)) return
			}
			
			showMessage("Вы выиграли!")
			init()
			rules()
		}
	}
}